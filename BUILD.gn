#
# Copyright (c) 2020 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

minimp4_sources = [ "minimp4.c" ]

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//build/lite/ndk/ndk.gni")
  defines = []

  config("minimp4_config") {
    include_dirs = [ "minimp4" ]
  }

  lite_library("minimp4_shared") {
    target_type = "shared_library"
    public_configs = [ ":minimp4_config" ]
    output_name = "minimp4"
    sources = minimp4_sources
  }

  lite_library("minimp4_static") {
    target_type = "static_library"
    public_configs = [ ":minimp4_config" ]
    if (ohos_kernel_type == "liteos_m") {
      include_dirs = [
        "//kernel/liteos_m/kal/posix/include",
        "//kernel/liteos_m/kernel/include",
        "//kernel/liteos_m/utils",
        "//third_party/musl/porting/liteos_m/kernel/include/",
      ]
    }
    output_name = "minimp4"
    sources = minimp4_sources
  }

  group("minimp4") {
    if (ohos_kernel_type == "liteos_m") {
      deps = [ ":minimp4_static" ]
    } else {
      deps = [ ":minimp4_shared" ]
    }
  }

  ndk_lib("minimp4_ndk") {
    if (ohos_kernel_type == "liteos_m") {
      lib_extension = ".a"
    } else {
      lib_extension = ".so"
    }
    deps = [ ":minimp4" ]
    head_files = [ "include" ]
  }
} else {
  import("//build/ohos.gni")
  config("minimp4_config") {
    include_dirs = [ "minimp4" ]
  }

  ohos_shared_library("minimp4_shared") {
    public_configs = [ ":minimp4_config" ]
    output_name = "minimp4"
    subsystem_name = "common"
    part_name = "dsoftbus"
    sources = minimp4_sources
  }

  group("minimp4") {
    deps = [ ":minimp4_shared" ]
  }
}
